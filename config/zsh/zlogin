# Stolen from prezto
#
# Executes commands at login post-zshrc.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

# Execute code that does not affect the current session in the background.
{
    # Compile the completion dump to increase startup speed.
    zcompdump="${ZDOTDIR:-$HOME}/.zcompdump"
    if [[ -s "$zcompdump" && (! -s "${zcompdump}.zwc" || "$zcompdump" -nt "${zcompdump}.zwc") ]]; then
        zcompile "$zcompdump"
    fi
} &!

{
    emulate -L zsh
    local exclude='.git|.txt|.md|.adoc|.svg|.zwc|.gif|.tags'
    setopt extended_glob
    for z in $fpath; do
        if [[ $z =~ 'Users|home' ]]; then
            for file in $z/**/*; do
                [[ -f $file ]] && [[ $file.zwc -nt $file || ! -w ${file:h} ]] && break
                [[ -f $file && ! $file =~ $exclude ]] && zcompile $file
                unset file
            done
        fi
    done

    zmodload -m -F zsh/files b:zf_mkdir
    for var in $XDG_CONFIG_HOME $XDG_CACHE_HOME $XDG_DATA_HOME; do
        zf_mkdir -p $var
    done
    zmodload -u zsh/files
}&!
